plugins {
    id "net.minecraftforge.gradle.forge" version "6f53277"
    id "com.github.johnrengelman.shadow" version "6.1.0"
    id "java"
}

version = projectVersion
group = projectGroup
archivesBaseName = projectName

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false
    replace("@NAME@", projectName)
    replace("@VER@": projectVersion)
    replace("@ID@", projectId)
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven { url "https://repo.sk1er.club/repository/maven-public/" }
}

dependencies {
    implementation("gg.essential:vigilance-1.8.9-forge:${vigilanceVersion}")
}

processResources {
    inputs.property("version", version)
    inputs.property("mcversion", minecraft.version)

    filesMatching("mcmod.info") {
        expand(
                "id": projectId,
                "name": projectName,
                "version": version,
                "mcversion": minecraft.version
        )
    }
}

task moveResources {
    doLast {
        /* We wrap this action in a try-catch because of potential failures while hotswapping. */
        try {
            ant.move(
                    file: "${buildDir}/resources/main",
                    todir: "${buildDir}/classes/java"
            )
        } catch (Exception e) {
            e.printStackTrace();
        }
    }
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    archiveClassifier.set("")
}

moveResources.dependsOn(processResources)
classes.dependsOn(moveResources)
jar.finalizedBy(shadowJar)